!
!------------------------------------------------------------------------------
!                   Author      :   Vikas sharma
!                   Position    :   Doctral Student
!                   Institute   :   Kyoto Univeristy, Japan
!                   Program name:   FextVector_5.part
!                   Last Update :   Nov-23-2017
!
!------------------------------------------------------------------------------
!                           Details of Program
!==============================================================================
!
!   TYPE    ::  Part of the Code
!
!   DESCRIPTION
!       -   This code is part of the code
!       -   Space Interpolation of scalar and vector
!
!   HOSTING FILE
!       -    STFextVector_Class.f90
!
!==============================================================================

!------------------------------------------------------------------------------
!                                                              getFextVector_5
!------------------------------------------------------------------------------

SUBROUTINE getFextVector_5( Obj, Fext, FextType )

  !.  .   .   .   .   .   .   .   .   .   .   .   .   .   .   .   .   .   .   .
  !    1. Fext is space nodal values and FextType can be nodal or quad
  !.  .   .   .   .   .   .   .   .   .   .   .   .   .   .   .   .   .   .   .
  
  USE Utility, ONLY : OUTERPROD

  ! Define intent of dummy variables
#ifdef STMat
  CLASS( SpaceTimeMatrix_ ), INTENT( INOUT ), TARGET ::  Obj
#else
  CLASS( STFextVector_ ), INTENT( INOUT ), TARGET ::  Obj
#endif

  REAL( DFP ), DIMENSION( :, : ), INTENT( IN ) :: Fext
  CHARACTER( LEN = * ), INTENT( IN ) :: FextType

  ! Define internal variables
  INTEGER( I4B ) :: NNS, NNT, NIPS, NIPT, IPS, IPT, a, M
  REAL( DFP ), ALLOCATABLE :: Fbar( : ), DummyMat( :, : )
  REAL( DFP ) :: RealVal
  LOGICAL( LGT ) :: isNodal
  CLASS( STShapeData_ ), POINTER :: SD

#ifdef DEBUG_VER
  Error_Flag = .FALSE.
  !   Flag-1
  IF( .NOT. Obj % isInitiated( ) ) THEN
     CALL Err_Msg("STFextVector_Class.f90", &
          "getFextVector_5(), Flag-1", &
          "STFextVector_ Object is not Initiated" )
     Error_Flag = .TRUE.
     RETURN
  END IF
#endif

  NNS = Obj % SD( 1,1 ) % getNNS( ) 
  NIPS = Obj % getNIPS( )
  NNT = Obj % SD( 1,1 ) % getNNT( )
  NIPT = Obj % getNIPT( )

#ifdef DEBUG_VER
  IF( SIZE( Fext, 2 ) .NE. NNS .AND. SIZE( Fext, 2 ) .NE. NIPS ) THEN
     CALL Err_Msg("STFextVector_Class.f90", &
          & "getFextVector_5(), Flag-2", &
          & "The SIZE( Fext, 2 ) should be &
          & equal to NNS or NIPS" )
     Error_Flag = .TRUE.
     RETURN
  END IF
#endif

  isNodal = .TRUE.
  SELECT CASE( TRIM( ADJUSTL( FextType ) ) )
  CASE( 'Quad', 'QuadPoints', 'Quad Points', &
       & 'Integration', 'Integration Points', &
       & 'IntegrationPoints')
     isNodal = .FALSE.
  END SELECT

  M = SIZE( Fext, 1 )
  ALLOCATE( Fbar( M ) )

  IF( ALLOCATED( Obj % Vec3 ) ) DEALLOCATE( Obj % Vec3 )
  ALLOCATE( Obj % Vec3( M, NNS, NNT ) )
  Obj % Vec3 = 0.0_DFP

  DO IPT = 1, NIPT
     DO IPS = 1, NIPS
        
        SD => Obj % SD( IPS, IPT )
        RealVal = SD % Ws * SD % Wt * SD % Js_Xi2Xt &
             & * SD % Jt * SD % Thickness
        
        IF( isNodal ) THEN
           CALL SD % getInterpolationOfVector( &
                & SpaceNodalValues = Fext, Val = Fbar )
        ELSE
           Fbar = Fext( :, IPS )
        END IF
        
        DummyMat = OUTERPROD( Fbar, SD % N )
        
        DO a = 1, NNT
           Obj % Vec3( :, :, a ) = Obj % Vec3( :, :, a ) + &
                & DummyMat * RealVal * SD % T( a )
        END DO
     END DO
  END DO

  DEALLOCATE( Fbar, DummyMat )
  NULLIFY( SD )

END SUBROUTINE getFextVector_5

!------------------------------------------------------------------------------
!
!------------------------------------------------------------------------------

