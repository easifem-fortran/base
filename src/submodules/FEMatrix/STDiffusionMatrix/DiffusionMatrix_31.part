!
!------------------------------------------------------------------------------
!                   Author      :   Vikas sharma
!                   Position    :   Doctral Student
!                   Institute   :   Kyoto Univeristy, Japan
!                   Program name:   DiffusionMatrix_31.part
!                   Last Update :   March-27-2018
!
!------------------------------------------------------------------------------
!                           Details of Program
!==============================================================================
!
!   TYPE    ::  Part of the Code
!
!   DESCRIPTION
!       -   This code is part of the code
!       -   Diffusion matrix for pressure wave equation for 
!           alpha-beta-v-STFEM
!
!   HOSTING FILE
!       -    STDiffusionMatrix_Class.f90
!==============================================================================

!------------------------------------------------------------------------------
!                                                        getDiffusionMatrix_31
!------------------------------------------------------------------------------

 SUBROUTINE getDiffusionMatrix_31( Obj, TimeVector, IntegrationSide, Beta_STFEM )

!.  .   .   .   .   .   .   .   .   .   .   .   .   .   .   .   .   .   .   .
!       1.  Returns Diffusion matrix for acoustic wave equation
!       2.  Time Integration is character "Right", "Left", "Both"
!.  .   .   .   .   .   .   .   .   .   .   .   .   .   .   .   .   .   .   .

    USE Utility, ONLY : OUTERPROD

  ! Define intent of dummy variables
#ifdef STMat
  CLASS( SpaceTimeMatrix_ ), INTENT( INOUT ), TARGET ::  Obj
#else
  CLASS( STDiffusionMatrix_ ), INTENT( INOUT ), TARGET ::  Obj    
#endif
    REAL( DFP ), INTENT( IN ) :: TimeVector( 2 ), Beta_STFEM
    CHARACTER( LEN = * ), INTENT( IN ) :: IntegrationSide

    ! Define internal variables
    INTEGER( I4B ) :: NNS, NNT, NIPS, NIPT, IPS, IPT, aa, b, NSD, &
                      i, j, M, p, q
    
    REAL( DFP ), ALLOCATABLE :: dNTdXt( :, :, : ), T( : ), dNTdXt2( :, :, : ), &
                                dNTdXt3( :, :, : )
    REAL( DFP ) :: Beta, Ws, Js, thick, RealVal, Jt, Wt, Kij, t1, t2, Theta
    TYPE( STElemShapeData_ ), TARGET :: STElemSD, STElemSD2
    CLASS( STShapeData_ ), POINTER :: SD => NULL( ), SD2 => NULL( )

    Error_Flag = .FALSE.

    IF( .NOT. Obj % isInitiated( ) ) THEN

        CALL Err_Msg( &
        "STDiffusionMatrix_Class.f90", &
        "getDiffusionMatrix_31()", &
        "STDiffusionMatrix_ Object is not Initiated" &
        )
        Error_Flag = .TRUE.
        RETURN
    
    END IF

    NNS = Obj % SD( 1,1 ) % getNNS( )
    NNT = Obj % SD( 1,1 ) % getNNT( )
    NSD = Obj % SD( 1, 1 ) % getNSD( )
    NIPS = Obj % getNIPS( )
    NIPT = Obj % getNIPT( )

    IF( ALLOCATED( Obj % Mat4 ) ) DEALLOCATE( Obj % Mat4 )
    ALLOCATE( Obj % Mat4( NNS, NNS, NNT, NNT ) )
    Obj % Mat4 = 0.0_DFP

    M = NSD

    t1 = TimeVector( 1 )
    t2 = TimeVector( 2 )

    Beta = Beta_STFEM * ( t2 - t1 )
    
    ! Make copy of Obj
    CALL STElemSD % Initiate( Obj )
    CALL STElemSD2 % Initiate( Obj )
    
    DO IPT = 1, NIPT

        DO IPS = 1, NIPS

            SD => STElemSD .SDPointer. [IPS, IPT]
            Theta = .Theta. SD
            T = SD .TimeIntegration. [t1, t2, Theta]

            CALL SD % setT( T )
            CALL Check_Error( &
            "STDiffusionMatrix_Class.f90>>DiffusionMatrix_31.part", &
            "Traceback ---> CALL SD % setT( T )"&
            )

            CALL SD % setdNTdXt( )
            CALL Check_Error( &
            "STDiffusionMatrix_Class.f90>>DiffusionMatrix_31.part", &
            "Traceback ---> CALL SD % setdNTdXT(  )"&
            )

            SD2 => STElemSD2 % SD( IPS, IPT )
            T  = SD2 % dTdTheta / SD2 % Jt

            CALL SD2 % setT( T )
            CALL Check_Error( &
            "STStiffnessMatrix_Class.f90>>StiffnessMatrix_13.part", &
            "Traceback ---> CALL SD2 % setT( T )"&
            )

            CALL SD2 % setdNTdXt( )
            CALL Check_Error( &
            "STStiffnessMatrix_Class.f90>>StiffnessMatrix_13.part", &
            "Traceback ---> CALL SD2 % setdNTdXT(  )"&
            )

        END DO

    END DO

    DO IPT = 1, NIPT

        DO IPS = 1, NIPS

            Ws = Obj % SD( IPS, IPT ) % getWs( )
            Wt = Obj % SD( IPS, IPT ) % getWt( )

            Js = Obj % SD( IPS, IPT ) % getJs_Xi2Xt( )
            Jt = Obj % SD( IPS, IPT ) % getJt( )
            
            thick = Obj % SD( IPS, IPT ) % getThickness( )
            
            CALL Obj % SD( IPS, IPT ) % getdNTdXt( dNTdXt )
            CALL STElemSD % SD( IPS, IPT ) % getdNTdXt( dNTdXt2 )
            CALL STElemSD2 % SD( IPS, IPT ) % getdNTdXt( dNTdXt3 )
            
            RealVal = Ws * Wt * Js * Jt * thick * Beta

            DO aa = 1, NNT

                DO b = 1, NNT

                    DO p = 1, NSD

                        SELECT CASE( TRIM( IntegrationSide ) )

                            CASE( "Right", "RIGHT", "right" )
                            Obj % Mat4( :, :, aa, b ) = &
                            Obj % Mat4( :, :, aa, b ) + &
                            OUTERPROD( a = dNTdXt3( :, p, aa ), &
                            b = dNTdXt2( :, p, b ) ) * RealVal

                            CASE( "Left", "LEFT", "left" )
                            Obj % Mat4( :, :, aa, b ) = &
                            Obj % Mat4( :, :, aa, b ) + &
                            OUTERPROD( a = dNTdXt2( :, p, aa ), &
                            b = dNTdXt3( :, p, b ) ) * RealVal

                            CASE( "None", "NONE", "none" )
                            Obj % Mat4( :, :, aa, b ) = &
                            Obj % Mat4( :, :, aa, b ) + &
                            OUTERPROD( a = dNTdXt3( :, p, aa ), &
                            b = dNTdXt( :, p, b ) ) * RealVal

                            CASE DEFAULT

                                CALL Err_Msg( &
                                "STDiffusionMatrix_Class.f90", &
                                "getStiffnessMatrix_31()", &
                                "No case found for given integration side" &
                                )
                                Error_Flag = .TRUE.
                                RETURN

                        END SELECT

                    END DO

                END DO

            END DO
        
        END DO

    END DO

    IF( ALLOCATED( Obj % Mat2 ) ) DEALLOCATE( Obj % Mat2 )
    IF( ALLOCATED( dNTdXt ) ) DEALLOCATE( dNTdXt )
    IF( ALLOCATED( dNTdXt2 ) ) DEALLOCATE( dNTdXt2 )
    IF( ALLOCATED( dNTdXt3 ) ) DEALLOCATE( dNTdXt3 )
    IF( ALLOCATED( T ) ) DEALLOCATE( T )
    CALL STElemSD % DeallocateData( )
    CALL STElemSD2 % DeallocateData( )
    SD => NULL( )
    SD2 => NULL( )

 END SUBROUTINE getDiffusionMatrix_31

!------------------------------------------------------------------------------
!
!------------------------------------------------------------------------------

