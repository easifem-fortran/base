!
!------------------------------------------------------------------------------
!                   Author      :   Vikas sharma
!                   Position    :   Doctral Student
!                   Institute   :   Kyoto Univeristy, Japan
!                   Program name:   ConvectiveMatrix_7.part
!                   Last Update :   Nov-20-2017
!
!------------------------------------------------------------------------------
!                           Details of Program
!==============================================================================
!
!   TYPE    ::  Part of the Code
!
!   DESCRIPTION
!       -   This code is part of the code
!
!   HOSTING FILE
!       -    ConvectiveMatrix_Class.f90
!
!==============================================================================

!------------------------------------------------------------------------------
!                                                        getConvectiveMatrix_7
!------------------------------------------------------------------------------

SUBROUTINE getConvectiveMatrix_7( Obj, Term1, Term2, XType )

  !.    .    .    .    .    .    .    .    .    .    .    .    .    .    .    .
  !       1.  -   Returns mass matrix; C is a 2D array of Space Nodal Values
  !               The first index is for spatial component and second index is 
  !               is for spatial nodal values.
  !.    .    .    .    .    .    .    .    .    .    .    .    .    .    .    .

    ! Define intent of dummy variables
#ifdef SpaceMat
    CLASS( SpaceMatrix_ ), INTENT( INOUT ), TARGET ::  Obj
#else
    CLASS( ConvectiveMatrix_ ), INTENT( INOUT ), TARGET ::  Obj
#endif
  INTEGER( I4B ), INTENT( IN ) :: Term1, Term2
  CHARACTER( LEN = * ), INTENT( IN ) :: XType

  ! Define internal variables
  INTEGER( I4B ) :: NNS, NIPS, IPS, NSD, Indx
  REAL( DFP ) :: RealVal

#ifdef DEBUG_VER
  Error_Flag = .FALSE.
  IF( .NOT. Obj % isInitiated( ) ) THEN
     CALL Err_Msg("ConvectiveMatrix_Class.f90", &
          & "getConvectiveMatrix_7(), Flag-1", &
          & "ConvectiveMatrix_ Object is not Initiated" )
     Error_Flag = .TRUE.
     RETURN
  END IF
#endif

  NNS = Obj % SD( 1 ) % getNNS( )
  NSD = Obj % SD( 1 ) % getNSD( )
  NIPS = Obj % getNIPS( )
  IF( ALLOCATED( Obj % Mat2 ) ) DEALLOCATE( Obj % Mat2 )
  ALLOCATE( Obj % Mat2( NNS, NNS ) )
  Obj % Mat2 = 0.0_DFP

  SELECT CASE( TRIM( ADJUSTL( XType ) ) ) 
  CASE( "dx", "dX", "dx1", "dX1", "x", "X", "x1", "X1" )
     Indx = 1_I4B
  CASE( "dy", "dY", "dx2", "dX2", "y", "Y", "x2", "X2" )
     Indx = 2_I4B
  CASE( "dz", "dZ", "dx3", "dX3", "z", "Z", "x3", "X3" )
     Indx = 3_I4B 
  CASE DEFAULT
     Indx = 0_I4B
  END SELECT

  DO IPS = 1, NIPS

     RealVal = Obj % SD( IPS ) % Ws &
          & * Obj % SD( IPS ) % Js_Xi2Xt &
          & * Obj % SD( IPS ) % Thickness

     IF( Term1 .EQ. 1 ) THEN
        Obj % Mat2( :, : ) = Obj % Mat2( :, : ) + &
             & OUTERPROD( a = Obj % SD( IPS ) %  dNdXt( :, Indx ), &
             & b = Obj % SD( IPS ) % N ) * RealVal
     ELSE
        Obj % Mat2( :, : ) = Obj % Mat2( :, : ) + &
             & OUTERPROD( a = Obj % SD( IPS ) % N, &
             & b =  Obj % SD( IPS ) %  dNdXt( :, Indx ) ) * RealVal
     END IF

  END DO

END SUBROUTINE getConvectiveMatrix_7

!------------------------------------------------------------------------------
!
!------------------------------------------------------------------------------

