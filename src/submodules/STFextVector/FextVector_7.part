!
!------------------------------------------------------------------------------
!                   Author      :   Vikas sharma
!                   Position    :   Doctral Student
!                   Institute   :   Kyoto Univeristy, Japan
!                   Program name:   FextVector_7.part
!                   Last Update :   Nov-23-2017
!
!------------------------------------------------------------------------------
!                           Details of Program
!==============================================================================
!
!   TYPE    ::  Part of the Code
!
!   DESCRIPTION
!       -   This code is part of the code
!       -   Space Interpolation of scalar and vector
!
!   HOSTING FILE
!       -    STFextVector_Class.f90
!
!==============================================================================

!------------------------------------------------------------------------------
!                                                              getFextVector_7
!------------------------------------------------------------------------------

SUBROUTINE getFextVector_7( Obj, Fext, C, FextType, CType )

  !.  .   .   .   .   .   .   .   .   .   .   .   .   .   .   .   .   .   .   .
  !     1. Fext and C denotes the space nodal values
  !     2. FextType and CType can be Nodal and Quad
  !.  .   .   .   .   .   .   .   .   .   .   .   .   .   .   .   .   .   .   .

  USE Utility, ONLY : OUTERPROD

  ! Define intent of dummy variables
#ifdef STMat
  CLASS( SpaceTimeMatrix_ ), INTENT( INOUT ), TARGET ::  Obj
#else
  CLASS( STFextVector_ ), INTENT( INOUT ), TARGET ::  Obj
#endif

  REAL( DFP ), DIMENSION( :, : ), INTENT( IN ) :: Fext, C
  CHARACTER( LEN = * ), INTENT( IN ) :: FextType, CType

  ! Define internal variables
  INTEGER( I4B ) :: NNS, NNT, NIPS, NIPT, IPS, IPT, a, M, NSD
  REAL( DFP ), ALLOCATABLE :: cdNTdXt( :, : ), Fbar( : )
  REAL( DFP ) :: RealVal
  LOGICAL( LGT ) :: isCNodal, isFNodal
  CLASS( STShapeData_ ), POINTER :: SD

#ifdef DEBUG_VER
  Error_Flag = .FALSE.
  IF( .NOT. Obj % isInitiated( ) ) THEN
     CALL Err_Msg("STFextVector_Class.f90", &
          "getFextVector_7(), Flag-1", &
          "STFextVector_ Object is not Initiated" )
     Error_Flag = .TRUE.
     RETURN
  END IF
#endif

  NNS = Obj % SD( 1,1 ) % getNNS( )
  NNT = Obj % SD( 1,1 ) % getNNT( ) 
  NIPS = Obj % getNIPS( )
  NIPT = Obj % getNIPT( )
  NSD = Obj % SD( 1,1 ) % getNSD( )

#ifdef DEBUG_VER
  IF( SIZE( Fext, 2 ) .NE. NNS .AND. SIZE( Fext, 2 ) .NE. NIPS ) THEN
     CALL Err_Msg("STFextVector_Class.f90", &
          "getFextVector_7(), Flag-2", &
          "The SIZE( Fext, 2 ) should be equal to NNS or NIPS" )
     Error_Flag = .TRUE.
     RETURN
  END IF
  IF( SIZE( C, 2 ) .NE. NNS .AND. SIZE( C, 2 ) .NE. NIPS ) THEN
     CALL Err_Msg("STFextVector_Class.f90", &
          "getFextVector_7(), Flag-3", &
          "The SIZE( C, 2 ) should be equal to NNS or NIPS" )
     Error_Flag = .TRUE.
     RETURN
  END IF
  IF( SIZE( C, 1 ) .NE. NSD ) THEN
     CALL Err_Msg("STFextVector_Class.f90", &
          "getFextVector_7(), Flag-4", &
          "The SIZE( C, 1 ) should be equal to NSD" )
     Error_Flag = .TRUE.
     RETURN
  END IF
#endif

  M = SIZE( Fext, 1 )
  ALLOCATE( Fbar( M ) )
  IF( ALLOCATED( Obj % Vec3 ) ) DEALLOCATE( Obj % Vec3 )
  ALLOCATE( Obj % Vec3( M, NNS, NNT ) )
  Obj % Vec3 = 0.0_DFP

  isFNodal = .TRUE.
  SELECT CASE( TRIM( ADJUSTL( FextType ) ) )
  CASE( 'Quad', 'QuadPoints', 'Quad Points', &
       & 'Integration', 'Integration Points', &
       & 'IntegrationPoints' )
     isFNodal = .FALSE.
  END SELECT

  isCNodal = .TRUE.
  SELECT CASE( TRIM( ADJUSTL( CType ) ) )
  CASE( 'Quad', 'QuadPoints', 'Quad Points', &
       & 'Integration', 'Integration Points', &
       & 'IntegrationPoints' )
     isCNodal = .FALSE.
  END SELECT

  DO IPT = 1, NIPT
     DO IPS = 1, NIPS

        SD=>Obj%SD(IPS,IPT)
        RealVal = SD % Ws * SD % Wt * SD % Js_Xi2Xt &
             & * SD % Jt * SD % Thickness

        IF( isCNodal ) THEN
           CALL SD % getProjectionOfdNTdXt( &
                & SpaceNodalValues = C, cdNTdXt = cdNTdXt )
        ELSE
           CALL SD % getProjectionOfdNTdXt( &
                & VectorValues = C(:,IPS), cdNTdXt = cdNTdXt )
        END IF

        IF( isFNodal ) THEN
           CALL SD % getInterpolationOfVector( &
                & SpaceNodalValues = Fext, Val = Fbar )
        ELSE
           Fbar = Fext( :, IPS )
        END IF

        DO a = 1, NSD
           Obj % Vec3( a, :, : )  = Obj % Vec3( a, :, : ) + &
                & RealVal * cdNTdXt * Fbar( a )
        END DO
     END DO
  END DO

  DEALLOCATE( Fbar, cdNTdXt )
  NULLIFY( SD )

END SUBROUTINE getFextVector_7
!
!------------------------------------------------------------------------------
!
!------------------------------------------------------------------------------
!
