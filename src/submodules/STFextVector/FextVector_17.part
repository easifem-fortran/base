!
!------------------------------------------------------------------------------
!                   Author      :   Vikas sharma
!                   Position    :   Doctral Student
!                   Institute   :   Kyoto Univeristy, Japan
!                   Program name:   FextVector_17.part
!                   Last Update :   Nov-24-2017
!
!------------------------------------------------------------------------------
!                           Details of Program
!==============================================================================
!
!   TYPE    ::  Part of the Code
!
!   DESCRIPTION
!       -   This code is part of the code
!       -   Space Interpolation of scalar and vector
!
!   HOSTING FILE
!       -    STFextVector_Class.f90
!
!==============================================================================

!------------------------------------------------------------------------------
!                                                              getFextVector_17
!------------------------------------------------------------------------------

SUBROUTINE getFextVector_17( Obj, Fext, C )

  !.  .   .   .   .   .   .   .   .   .   .   .   .   .   .   .   .   .   .   .
  !     1. Both Fext and C are constant in space and time
  !.  .   .   .   .   .   .   .   .   .   .   .   .   .   .   .   .   .   .   .

  ! Define intent of dummy variables
#ifdef STMat
  CLASS( SpaceTimeMatrix_ ), INTENT( INOUT ), TARGET ::  Obj
#else
  CLASS( STFextVector_ ), INTENT( INOUT ), TARGET ::  Obj
#endif

  REAL( DFP ), DIMENSION( : ), INTENT( IN ) :: Fext, C

  ! Define internal variables
  INTEGER( I4B ) :: NNS, NNT, NIPS, NIPT, IPS, IPT, a, M, NSD
  REAL( DFP ), ALLOCATABLE :: cdNTdXt( :, : )
  REAL( DFP ) :: RealVal
  CLASS( STShapeData_ ), POINTER :: SD

#ifdef DEBUG_VER    
  Error_Flag = .FALSE.
  IF( .NOT. Obj % isInitiated( ) ) THEN
     CALL Err_Msg("STFextVector_Class.f90", &
          "getFextVector_17(), Flag-1", &
          "STFextVector_ Object is not Initiated" )
     Error_Flag = .TRUE.
     RETURN
  END IF
#endif

  NNS = Obj % SD( 1,1 ) % getNNS( )
  NNT = Obj % SD( 1,1 ) % getNNT( ) 
  NIPS = Obj % getNIPS( )
  NIPT = Obj % getNIPT( )
  NSD = Obj % SD( 1,1 ) % getNSD( )

#ifdef DEBUG_VER
  IF( SIZE( C, 1 ) .NE. NSD ) THEN
     CALL Err_Msg("STFextVector_Class.f90", &
          "getFextVector_17(), Flag-2", &
          "The SIZE( C, 1 ) should be equal to NSD" )
     Error_Flag = .TRUE.
     RETURN
  END IF
#endif

  M = SIZE( Fext, 1 )
  IF( ALLOCATED( Obj % Vec3 ) ) DEALLOCATE( Obj % Vec3 )
  ALLOCATE( Obj % Vec3( M, NNS, NNT ) )
  Obj % Vec3 = 0.0_DFP

  DO IPT = 1, NIPT
     DO IPS = 1, NIPS

        SD => Obj%SD(IPS,IPT)
        RealVal = SD % Ws * SD % Wt * SD % Js_Xi2Xt &
             & * SD % Jt * SD % Thickness
        CALL SD % getProjectionOfdNTdXt( &
             & VectorValues = C, cdNTdXt = cdNTdXt )
        DO a = 1, NSD
           Obj % Vec3( a, :, : )  = Obj % Vec3( a, :, : ) + &
                & RealVal * cdNTdXt * Fext( a )
        END DO

     END DO
  END DO

  DEALLOCATE( cdNTdXt )
  NULLIFY( SD )

END SUBROUTINE getFextVector_17

!------------------------------------------------------------------------------
!
!------------------------------------------------------------------------------
