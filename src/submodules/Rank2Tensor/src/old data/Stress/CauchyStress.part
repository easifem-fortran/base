!
!------------------------------------------------------------------------------
!                   Author      :   Vikas sharma
!                   Position    :   Doctral Student
!                   Institute   :   Kyoto Univeristy, Japan
!                   Program name:   CauchyStress.part
!                   Last Update :   Dec-28-2017
!
!------------------------------------------------------------------------------
!                       Details of Program
!==============================================================================
!
!   Type:: Part of Module
!
!   Description ::
!       -   This part includes subroutine for getting Cauchy Stress from
!           any other Stress Measures
!   Hosting File ::
!       -   Stress_Class
!==============================================================================

!------------------------------------------------------------------------------
!                                                              getCauchyStress
!------------------------------------------------------------------------------

 FUNCTION getCauchyStress( obj, F )

!.  .   .   .   .   .   .   .   .   .   .   .   .   .   .   .   .   .   .   .
!       1.  This subroutine computes the Cauchy stress from given stress type
!       2.  Fobj is Deformation Gradient object.
!.  .   .   .   .   .   .   .   .   .   .   .   .   .   .   .   .   .   .   .

    USE Rank2Tensor_Class
    USE DeformationGradient_Class

    !  Define arguments of dummy argument
    CLASS( Stress_ ), INTENT( IN ) :: obj
    TYPE( DeformationGradient_ ), INTENT( IN ) :: F
    REAL( DFP ), ALLOCATABLE :: getCauchyStress( :, : )

    ! Define internal variables
    TYPE( Rank2Tensor_ ) :: T
    REAL( DFP ) :: J

    IF( .NOT. ALLOCATED( obj%V ) ) THEN

        CALL Err_Msg( &
            "Stress_Class.f90>>CauchyStress.part", &
            "getCauchyStress()", &
            "Stress_ object is not initiated. &
            Program Stopped !!!" &
        )
        STOP

    END IF

    T = obj

    SELECT CASE( TRIM( obj%StressType ) )

        CASE( "Cauchy", "cauchy", "Sigma", "sigma" )

            getCauchyStress = T

        CASE( "Kirchhoff", "kirchhoff", "Tau", "kirchoff", "Kirchoff", "tau" )

            J = .det. F

            getCauchyStress = T * ( 1.0_DFP / J )

        CASE( "PK2", "Pk2", "pk2", "PK-2", "pk-2", "S", "s" )

            J = .det. F
            getCauchyStress  = ( F .matmul. T ) .matmul. ( .transpose. F )
            getCauchyStress = getCauchyStress * ( 1.0_DFP / J )

        CASE( "PK1", "Pk1", "pk1", "PK-1", "pk-1", "Pi", "PI", "pi" )

            J = .det. F
            getCauchyStress = T .matmul. ( .transpose. F )
            getCauchyStress = getCauchyStress * ( 1.0_DFP / J )

        CASE( "Eshelby-Like", "Eshelby", "EshelbyLike", "M", "m" )

            J = .det. F
            getCauchyStress = ( ( .transpose. ( .inv. F ) ) .matmul. T ) .matmul. ( .transpose. F )
            getCauchyStress = getCauchyStress * ( 1.0_DFP / J )


        CASE DEFAULT

            CALL Err_Msg( &
                "Stress_Class.f90>>CauchyStress.part", &
                "getCauchyStress( obj, F )", &
                "No case found for obj%StressType. &
                Program Stopped!!!" &
            )
            STOP

    END SELECT

    CALL T%DeallocateData( )

 END FUNCTION getCauchyStress

!------------------------------------------------------------------------------
!
!------------------------------------------------------------------------------

